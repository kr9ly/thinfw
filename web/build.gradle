buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.5"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: "net.ltgt.apt"
apply plugin: 'org.akhikhl.gretty'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    coreSrcDir = file("${projectDir}/src/core/java")
    coreResourcesDir = file("${projectDir}/src/core/resources")
    envSrcDir = file("${projectDir}/src/${buildEnv}/java")
    envResourcesDir = file("${projectDir}/src/${buildEnv}/resources")
}

gretty {
    contextPath = '/'
}

sourceSets {
    main {
        java.srcDirs += coreSrcDir
        resources.srcDirs += coreResourcesDir
        java.srcDirs += envSrcDir
        resources.srcDirs += envResourcesDir
    }
}

repositories {
    mavenCentral()
    maven { url 'http://kr9ly.github.io/maven/' }
}

dependencies {
    compile project(':common')
    compile project(':conf')
    compile 'com.sparkjava:spark-core:2.3'
    compile 'com.sparkjava:spark-template-pebble:1.0.0'
    apt 'net.kr9ly:doubler-compiler:0.9.0'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
}

idea {
    module {
        sourceDirs += file("${projectDir}/generated")
        generatedSourceDirs += file("${projectDir}/generated")
    }
}