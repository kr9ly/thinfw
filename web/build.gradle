buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    provided
}

ext {
    coreSrcDir = file("${projectDir}/src/core/java")
    coreResourcesDir = file("${projectDir}/src/core/resources")
    envSrcDir = file("${projectDir}/src/${buildEnv}/java")
    envResourcesDir = file("${projectDir}/src/${buildEnv}/resources")
    generatedSourcesDir = file("${projectDir}/generated/main/src")
}

gretty {
    contextPath = '/'
}

sourceSets {
    main {
        java.srcDirs += coreSrcDir
        resources.srcDirs += coreResourcesDir
        java.srcDirs += envSrcDir
        resources.srcDirs += envResourcesDir
        compileClasspath += configurations.provided
    }
}

repositories {
    mavenCentral()
    maven { url 'http://kr9ly.github.io/maven/' }
}

compileJava {
    doFirst {
        generatedSourcesDir.mkdirs()
    }
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-s', generatedSourcesDir]
}

dependencies {
    compile project(':common')
    compile project(':conf')
    compile 'com.sparkjava:spark-core:2.3'
    compile 'com.sparkjava:spark-template-pebble:1.0.0'
    provided 'net.kr9ly:doubler-compiler:0.9.0'
    provided 'com.google.dagger:dagger-compiler:2.0.2'
}

idea {
    module {
        sourceDirs += generatedSourcesDir
        generatedSourceDirs += generatedSourcesDir
        scopes.PROVIDED.plus += [configurations.provided]
    }
}