buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
        classpath 'io.github.robwin:swagger2markup-gradle-plugin:0.9.1'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'io.github.robwin.swagger2markup'
apply plugin: 'org.asciidoctor.convert'

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    provided
}

ext {
    coreSrcDir = file("${projectDir}/src/core/java")
    coreResourcesDir = file("${projectDir}/src/core/resources")
    envSrcDir = file("${projectDir}/src/${buildEnv}/java")
    envResourcesDir = file("${projectDir}/src/${buildEnv}/resources")
    generatedSourcesDir = file("${projectDir}/generated/main/src")
}

gretty {
    contextPath = '/'
    integrationTestTask = 'test'
}

convertSwagger2markup {
    inputDir file("/docs/swagger")
    outputDir file("/docs/asciidoc/generated")
    separatedDefinitions false
}

asciidoctor {
    sources {
        include "index.adoc"
    }
    sourceDir file("docs/asciidoc")
    outputDir file("/docs/api")
    backends = ['html5']
    attributes = [
            doctype: 'book',
            toc: 'left',
            toclevels: '2',
            numbered: '',
            sectlinks: '',
            sectanchors: ''
    ]
}

sourceSets {
    main {
        java.srcDirs += coreSrcDir
        resources.srcDirs += coreResourcesDir
        java.srcDirs += envSrcDir
        resources.srcDirs += envResourcesDir
        compileClasspath += configurations.provided
    }
}

repositories {
    mavenCentral()
    maven { url 'http://kr9ly.github.io/maven/' }
}

compileJava {
    doFirst {
        generatedSourcesDir.mkdirs()
    }
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-s', generatedSourcesDir]
}

dependencies {
    compile project(':common')
    compile project(':conf')
    compile 'com.sparkjava:spark-core:2.3'
    compile 'com.sparkjava:spark-template-pebble:1.0.0'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.22.1'
    compile 'org.glassfish.jersey.ext:jersey-mvc:2.22.1'
    compile 'com.mitchellbosecke:pebble:2.0.0'
    compile 'io.swagger:swagger-jersey2-jaxrs:1.5.0'
    provided 'net.kr9ly:doubler-compiler:1.0.0'
    provided 'com.google.dagger:dagger-compiler:2.0.2'
    testCompile 'junit:junit:4.12'
}

idea {
    module {
        sourceDirs += generatedSourcesDir
        generatedSourceDirs += generatedSourcesDir
        scopes.PROVIDED.plus += [configurations.provided]
    }
}